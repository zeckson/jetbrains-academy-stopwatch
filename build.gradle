buildscript {
    ext.kotlin_version = "1.4.20"
    ext.robolectric_version = "4.3.1"
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    afterEvaluate {
        if (extensions.findByName("android") != null) {
            android {
                testOptions {
                    unitTests {
                        includeAndroidResources = true
                        all {
                            afterTest { TestDescriptor test, TestResult result ->
                                if (result.resultType == TestResult.ResultType.FAILURE) {
                                    def message = result.exception?.message ?: "Wrong answer"
                                    def lines = message.readLines()
                                    println "#educational_plugin FAILED + " + lines[0]
                                    lines.subList(1, lines.size()).forEach { line ->
                                        println "#educational_plugin" + line
                                    }
                                    // we need this to separate output of different tests
                                    println()
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
